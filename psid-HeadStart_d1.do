****************************
*** psid-HeadStart_d1.do ***
****************************

capture log close
log using psid-HeadStart_d1.log, replace
set linesize 255
set more off
set varabbrev off

clear all
use J2555227.dta

********************************************************************
*** This program uses "renpsid" (a program written below) to     ***
*** rename the original PSID variables using the format:         ***
***                                                              ***
*** i_varname_yyyy - individual-level variables for year "yyyy"  ***
*** f_varname_yyyy - family-level variables for year "yyyy"      ***
*** h_varname_yyyy - Head of family variables for year "yyyy"    ***
*** w_varname_yyyy - Wife of head variables for year "yyyy"      ***
*** ------------------------------------------------------------ ***
*** Program "renpsid" - Rename PSID variables                    ***
*** Syntax - renpsid varname varlist                             ***
*** ------------------------------------------------------------ ***
*** varlist is PSID variable names in the format [yy]varname.    ***
*** For example: [96]ER9247 is the state of residence in 1996.   ***
*** ------------------------------------------------------------ ***
*** Example:                                                     ***
***    renpsid psid_state [96]ER9247 [01]ER17004 [03]ER21003     ***
***    is the same as typing:                                    ***
***                                                              ***
***     rename ER9247  psid_state_1996                           ***
***     rename ER17004 psid_state_2001                           ***
***     rename ER21003 psid_state_2003                           ***
********************************************************************

program define renpsid
   args varname tokenize `varlist'
   local stub "`1'"
   macro shift 1
   local psid_names "`*'"
   foreach psid_var of local psid_names {
      local yy   = substr("`psid_var'",2,2)
      if `yy'>=68 {
         local yyyy = "19" + "`yy'"
         }
      else {
         local yyyy = "20" + "`yy'"
      }
      local var = substr("`psid_var'",5,.)
      rename `var' `stub'_`yyyy'
      local lbl : variable label `stub'_`yyyy'
      label variable `stub'_`yyyy' `"`lbl' (`var')"'
      di "`var' is now `stub'_`yyyy' " _column(40) `"(`: var label `stub'_`yyyy'')"'
   }
end

*******************
*** PSID Sample ***
*******************

   gen byte PSIDSample  = .
   replace  PSIDSample  = 1 if ER30001>=1    & ER30001<=2930
   replace  PSIDSample  = 2 if ER30001>=3001 & ER30001<=3441
   replace  PSIDSample  = 3 if ER30001>=3442 & ER30001<=3511
   replace  PSIDSample  = 4 if ER30001>=5001 & ER30001<=6872
   replace  PSIDSample  = 5 if ER30001>=7001 & ER30001<=9043
   replace  PSIDSample  = 6 if ER30001>=9044 & ER30001<=9308
   label variable PSIDSample "PSID sample"
   assert PSIDSample~=.

#delimit ;
   label define PSIDSample
                1  "SRC cross-section sample"
                2  "Immigrant sample (1997)"
                3  "Immigrant sample (1999)"
                4  "1968 Census sample"
                5  "Latino sample (1990)"
                6  "Latino sample (1992)";
   label values PSIDSample PSIDSample;
#delimit cr;

**********************************
*** Individual-Level Variables ***
**********************************

***********************************************************
*** 1968 INTERVIEW NUMBER                               ***
*** PERSON NUMBER                                       ***
*** --------------------------------------------------- ***
*** [all]ER30001                                        ***
*** [all]ER30002                                        ***
***                                                     ***
*** SRC cross-section sample (1 - 2,930)                ***
*** 1968 Census sample (5,001 - 6,872)                  ***
*** Immigrant or Latino samples                         ***
***    (ER30001=3001-3511, 7001-9308)                   ***
***                                                     ***
*** To identify an individual across waves use the 1968 ***
*** ID and Person Number (Summary Variables ER30001 and ***
*** ER30002).                                           ***
***********************************************************

   gen person_ID = (ER30001 * 1000) + ER30002
   label variable person_ID "Unique Person ID (num)"
   duplicates report person_ID
   assert r(N) == r(unique_value)

   rename ER30001 f_interviewNum_1968
   rename ER30002 i_PersonNum_1968

********************************************************************
*** SEQUENCE NUMBER                                              ***
*** ------------------------------------------------------------ ***
*** [69]ER30021 [70]ER30044 [71]ER30068 [72]ER30092 [73]ER30118  ***
*** [74]ER30139 [75]ER30161 [76]ER30189 [77]ER30218 [78]ER30247  ***
*** [79]ER30284 [80]ER30314 [81]ER30344 [82]ER30374 [83]ER30400  ***
*** [84]ER30430 [85]ER30464 [86]ER30499 [87]ER30536 [88]ER30571  ***
*** [89]ER30607 [90]ER30643 [91]ER30690 [92]ER30734 [93]ER30807  ***
*** [94]ER33102 [95]ER33202 [96]ER33302 [97]ER33402 [99]ER33502  ***
*** [01]ER33602 [03]ER33702 [05]ER33802 [07]ER33902 [09]ER34002  ***
*** [11]ER34102 [13]ER34202 [15]ER34302                          ***
**** ----------------------------------------------------------- ***
*** 1-20  Individuals in the family at the time of the interview ***
*** 51-89 See codebook                                           ***
*** 0     Inap.                                                  ***
*** .     non-interview                                          ***
********************************************************************

   renpsid i_sequence ///
           [69]ER30021 [70]ER30044 [71]ER30068 [72]ER30092 [73]ER30118  ///
           [74]ER30139 [75]ER30161 [76]ER30189 [77]ER30218 [78]ER30247  ///
           [79]ER30284 [80]ER30314 [81]ER30344 [82]ER30374 [83]ER30400  ///
           [84]ER30430 [85]ER30464 [86]ER30499 [87]ER30536 [88]ER30571  ///
           [89]ER30607 [90]ER30643 [91]ER30690 [92]ER30734 [93]ER30807  ///
           [94]ER33102 [95]ER33202 [96]ER33302 [97]ER33402 [99]ER33502  ///
           [01]ER33602 [03]ER33702 [05]ER33802 [07]ER33902 [09]ER34002  ///
           [11]ER34102 [13]ER34202 [15]ER34302

*******************************************************************
*** RELATIONSHIP TO HEAD                                        ***
*** ----------------------------------------------------------- ***
*** [68]ER30003 [69]ER30022 [70]ER30045 [71]ER30069 [72]ER30093 ***
*** [73]ER30119 [74]ER30140 [75]ER30162 [76]ER30190 [77]ER30219 ***
*** [78]ER30248 [79]ER30285 [80]ER30315 [81]ER30345 [82]ER30375 ***
*** [83]ER30401 [84]ER30431 [85]ER30465 [86]ER30500 [87]ER30537 ***
*** [88]ER30572 [89]ER30608 [90]ER30644 [91]ER30691 [92]ER30735 ***
*** [93]ER30808 [94]ER33103 [95]ER33203 [96]ER33303 [97]ER33403 ***
*** [99]ER33503 [01]ER33603 [03]ER33703 [05]ER33803 [07]ER33903 ***
*** [09]ER34003 [11]ER34103 [13]ER34203 [15]ER34303             ***
*** ----------------------------------------------------------- ***
*** 10    Current head                                          ***
*** 20    Wife or new wife                                      ***
*** 22    "Wife" or new "wife"                                  ***
*** 30-98 See codebook                                          ***
*** 0     Inap.: respondent is not head/wife/"wife"             ***
*** .     non-interview                                         ***
*** ----------------------------------------------------------- ***
*** From codebook (IND2013ER_codebook.pdf): to                  ***
*** select current Heads, the user must select                  ***
*** those coded 10 in this variable whose sequence              ***
*** numbers = 01                                                ***
*******************************************************************

   renpsid i_relate ///
           [68]ER30003 [69]ER30022 [70]ER30045 [71]ER30069 [72]ER30093 ///
           [73]ER30119 [74]ER30140 [75]ER30162 [76]ER30190 [77]ER30219 ///
           [78]ER30248 [79]ER30285 [80]ER30315 [81]ER30345 [82]ER30375 ///
           [83]ER30401 [84]ER30431 [85]ER30465 [86]ER30500 [87]ER30537 ///
           [88]ER30572 [89]ER30608 [90]ER30644 [91]ER30691 [92]ER30735 ///
           [93]ER30808 [94]ER33103 [95]ER33203 [96]ER33303 [97]ER33403 ///
           [99]ER33503 [01]ER33603 [03]ER33703 [05]ER33803 [07]ER33903 ///
           [09]ER34003 [11]ER34103 [13]ER34203 [15]ER34303

**************************************************************
*** Create "currentHead_yy" and "currentWife_yy" varaibles ***
**************************************************************

   gen currentHead_1968 =  i_relate_1968==1
   gen currentWife_1968 =  i_relate_1968==2
   label variable currentHead_1968 "Head in 1968"
   label variable currentWife_1968 "Wife in 1968"

foreach yy of numlist 1969/1982 {
   assert i_relate_`yy'>=0 & i_relate_`yy'<=9
   gen currentHead_`yy' =  i_relate_`yy'==1  & i_sequence_`yy'==1
   gen currentWife_`yy' =  i_relate_`yy'==2  & i_sequence_`yy'==2
   assert (i_sequence_`yy'==2 | i_sequence_`yy'>50) ///
       if  i_relate_`yy'==2
label variable currentHead_`yy' "Head in `yy'"
label variable currentWife_`yy' "Wife in `yy'"
}

foreach yy of numlist 1983/1997 1999(2)2015 {
   assert i_relate_`yy'==0 | (i_relate_`yy'>=10 & i_relate_`yy'<=98)
   gen currentHead_`yy' =  i_relate_`yy'==10 & i_sequence_`yy'==1
   gen currentWife_`yy' = (i_relate_`yy'==20 | i_relate_`yy'==22) & ///
                           i_sequence_`yy'==2
   assert (i_sequence_`yy'==2 | i_sequence_`yy'>50) ///
       if (i_relate_`yy'==20  | i_relate_`yy'==22)
label variable currentHead_`yy' "Head in `yy'"
label variable currentWife_`yy' "Wife in `yy'"
}

****************************************
*** Create "childOfHead_yy" varaible ***
****************************************

   gen childOfHead_1968 = i_relate_1968==3
   label variable childOfHead_1968 "Child of head in `yy'"

foreach yy of numlist 1969/1982 {
   gen childOfHead_`yy' = i_relate_`yy'==3 & i_sequence_`yy'<50
   label variable childOfHead_`yy' "Child of head in `yy'"
}

foreach yy of numlist 1983/1997 1999(2)2015 {
   gen childOfHead_`yy' = i_relate_`yy'==30 & i_sequence_`yy'<50
   label variable childOfHead_`yy' "Child of head in `yy'"
}

******************************************
*** Create "relatedToHead_yy" varaible ***
*** (parent, child, sibling of head)   ***
******************************************

   gen relatedToHead_1968 = inlist(i_relate_1968,3,4,5)
   label variable relatedToHead_1968 "Related to head in `yy'"

foreach yy of numlist 1969/1982 {
   gen relatedToHead_`yy' = inlist(i_relate_`yy',3,4,5) & i_sequence_`yy'<50
   label variable relatedToHead_`yy' "Related to head in `yy'"
}

foreach yy of numlist 1983/1997 1999(2)2015 {
   gen relatedToHead_`yy' = inlist(i_relate_`yy',30,40,50) & i_sequence_`yy'<50
   label variable relatedToHead_`yy' "Related to head in `yy'"
}

**************************************************************
*** 1968 ID OF MOTHER / PERSON # OF MOTHER                 ***
*** 1968 ID OF FATHER / PERSON # OF FATHER                 ***
*** ------------------------------------------------------ ***
*** [all]ER32009 [all]ER32010                              ***
*** [all]ER32016 [all]ER32017                              ***
***                                                        ***
*** The actual 1968 ID number (the family-level identifier ***
*** which, when combined with person number, uniquely      ***
*** identifies an individual) of the individual's natural  ***
*** mother/father is coded.                                ***
**************************************************************

   gen mother_ID = (ER32009 * 1000) + ER32010 if ER32009~=0
   label variable mother_ID "Mother ID (num)"
   duplicates report mother_ID if mother_ID~=.

   gen father_ID = (ER32016 * 1000) + ER32017 if ER32016~=0
   label variable father_ID "Father ID (num)"
   duplicates report father_ID if father_ID~=.

******************************************
***  MARITAL STATUS OF MOTHER AT BIRTH ***
*** ---------------------------------- ***
*** [all]ER32015                       ***
******************************************

   rename ER32015 i_marstat_mother

******************************************
***  YEAR MOTHER BORN                  ***
*** ---------------------------------- ***
*** [all]ER32011                       ***
******************************************

   rename ER32011 i_mother_birthyear

*********************************
*** ORDER OF BIRTH TO MOTHER  ***
*** ------------------------- ***
*** [all]ER32013              ***
*********************************

   rename ER32013 i_birthorder

*************************************************
***  TOTAL # CHILDREN BORN TO MOTHER / FATHER ***
*** ----------------------------------------- ***
*** [all]ER32012 [all]ER32019                 ***
*************************************************

   rename ER32012 i_siblings_maternal
   rename ER32019 i_siblings_paternal

*********************************
*** YEAR 1ST/ONLY CHILD BORN  ***
*** ------------------------- ***
*** [all]ER32024              ***
*********************************

   rename ER32024 i_firstbirth

****************************************
*** # LIVE BIRTHS TO THIS INDIVIDUAL ***
*** -------------------------------- ***
*** [all]ER32022                     ***
****************************************

   rename ER32022 i_births

***************************
*** SEX OF INDIVIDUAL   ***
*** ------------------- ***
*** [all]ER32000        ***
*** ------------------- ***
*** 1 Male              ***
*** 2 Female            ***
*** 9 NA                ***
***************************

   rename ER32000  i_sex
   gen female              = (i_sex==2) if i_sex~=9
   label variable female "Female"

************************************************************************
*** BIRTH WEIGHT OF THIS INDIVIDUAL                                  ***
*** ---------------------------------------------------------------- ***
*** [all] ER32014                                                    ***
*** ---------------------------------------------------------------- ***
*** 16 - 224    Actual birth weight in ounces                        ***
*** 991 Actual weight unknown, but less than 5.5 pounds (88 ounces)  ***
*** 995 Actual weight unknown, but 5.5 pounds (88 ounces) or more    ***
*** 998 DK / NA                                                      ***
*** 999 No information was gathered about the identity of either     ***
***     parent (ER32009=0 and ER32016=0)/values for ER32009-ER32010  ***
***     obtained only from 1983 and 1984 coding of mother and no     ***
***     information was gathered in 1985-2015 about identity of      ***
***     father (ER32009=>0 and ER32011=0 and ER32016=0)              ***
************************************************************************

   rename ER32014  i_birthweight

*******************************************************************
*** AGE OF INDIVIDUAL                                           ***
*** ----------------------------------------------------------- ***
*** [68]ER30004 [69]ER30023 [70]ER30046 [71]ER30070 [72]ER30094 ***
*** [73]ER30120 [74]ER30141 [75]ER30163 [76]ER30191 [77]ER30220 ***
*** [78]ER30249 [79]ER30286 [80]ER30316 [81]ER30346 [82]ER30376 ***
*** [83]ER30402 [84]ER30432 [85]ER30466 [86]ER30501 [87]ER30538 ***
*** [88]ER30573 [89]ER30609 [90]ER30645 [91]ER30692 [92]ER30736 ***
*** [93]ER30809 [94]ER33104 [95]ER33204 [96]ER33304 [97]ER33404 ***
*** [99]ER33504 [01]ER33604 [03]ER33704 [05]ER33804 [07]ER33904 ***
*** [09]ER34004 [11]ER34104 [13]ER34204 [15]ER34305             ***
*** ----------------------------------------------------------- ***
*** 1-125 Actual age                                            ***
*** 999   NA/DK                                                 ***
*** 0     Inap.                                                 ***
*******************************************************************

   renpsid i_age ///
           [68]ER30004 [69]ER30023 [70]ER30046 [71]ER30070 [72]ER30094  ///
           [73]ER30120 [74]ER30141 [75]ER30163 [76]ER30191 [77]ER30220  ///
           [78]ER30249 [79]ER30286 [80]ER30316 [81]ER30346 [82]ER30376  ///
           [83]ER30402 [84]ER30432 [85]ER30466 [86]ER30501 [87]ER30538  ///
           [88]ER30573 [89]ER30609 [90]ER30645 [91]ER30692 [92]ER30736  ///
           [93]ER30809 [94]ER33104 [95]ER33204 [96]ER33304 [97]ER33404  ///
           [99]ER33504 [01]ER33604 [03]ER33704 [05]ER33804 [07]ER33904  ///
           [09]ER34004 [11]ER34104 [13]ER34204 [15]ER34305

*******************************************************************
*** YEAR INDIVIDUAL BORN                                        ***
*** ----------------------------------------------------------- ***
*** [83]ER30404 [84]ER30434 [85]ER30468 [86]ER30503 [87]ER30540 ***
*** [88]ER30575 [89]ER30611 [90]ER30647 [91]ER30694 [92]ER30738 ***
*** [93]ER30811 [94]ER33106 [95]ER33206 [96]ER33306 [97]ER33406 ***
*** [99]ER33506 [01]ER33606 [03]ER33706 [05]ER33806 [07]ER33906 ***
*** [09]ER34006 [11]ER34106 [13]ER34206 [15]ER34307             ***
*******************************************************************

   renpsid i_birthyear ///
           [83]ER30404 [84]ER30434 [85]ER30468 [86]ER30503 [87]ER30540  ///
           [88]ER30575 [89]ER30611 [90]ER30647 [91]ER30694 [92]ER30738  ///
           [93]ER30811 [94]ER33106 [95]ER33206 [96]ER33306 [97]ER33406  ///
           [99]ER33506 [01]ER33606 [03]ER33706 [05]ER33806 [07]ER33906  ///
           [09]ER34006 [11]ER34106 [13]ER34206 [15]ER34307

**********************************************************
*** Individual-Level Variables from 1995 PSID          ***
*** -------------------------------------------------- ***
*** [95]ER33261  - EVER ENROLLED IN HEAD START 95      ***
*** [95]ER33262  - HOW OLD 1ST ATTEND HEAD START 95    ***
*** [95]ER33264  - OTHER NURSERY PRESCHOOL DAY CARE 95 ***
*** [95]ER33266  - EVER BOOKED/CHARGED BREAKING LAW 95 ***
**********************************************************

   rename ER33261 i_headstart_1995
   rename ER33262 i_headstartage_1995
   rename ER33264 i_preschool_1995
   rename ER33266 i_crime_1995

*******************************************************
*** YEARS COMPLETED EDUCATION 1968 - 2015           ***
*** ----------------------------------------------- ***
*** [68]ER30010 [70]ER30052 [71]ER30076 [72]ER30100 ***
*** [73]ER30126 [74]ER30147 [75]ER30169 [76]ER30197 ***
*** [77]ER30226 [78]ER30255 [79]ER30296 [80]ER30326 ***
*** [81]ER30356 [82]ER30384 [83]ER30413 [84]ER30443 ***
*** [85]ER30478 [86]ER30513 [87]ER30549 [88]ER30584 ***
*** [89]ER30620 [90]ER30657 [91]ER30703 [92]ER30748 ***
*** [93]ER30820 [94]ER33115 [95]ER33215 [96]ER33315 ***
*** [97]ER33415 [99]ER33516 [01]ER33616 [03]ER33716 ***
*** [05]ER33817 [07]ER33917 [09]ER34020 [11]ER34119 ***
*** [13]ER34230 [15]ER34349                         ***
*** ----------------------------------------------- ***
*** 1-17 Highest grade or year of school completed  ***
*** 98   DK                                         ***
*** 99   NA                                         ***
*** 0    Inap.: see codebook                        ***
*******************************************************

   renpsid i_yearseduc ///
           [68]ER30010 [70]ER30052 [71]ER30076 [72]ER30100 ///
           [73]ER30126 [74]ER30147 [75]ER30169 [76]ER30197 ///
           [77]ER30226 [78]ER30255 [79]ER30296 [80]ER30326 ///
           [81]ER30356 [82]ER30384 [83]ER30413 [84]ER30443 ///
           [85]ER30478 [86]ER30513 [87]ER30549 [88]ER30584 ///
           [89]ER30620 [90]ER30657 [91]ER30703 [92]ER30748 ///
           [93]ER30820 [94]ER33115 [95]ER33215 [96]ER33315 ///
           [97]ER33415 [99]ER33516 [01]ER33616 [03]ER33716 ///
           [05]ER33817 [07]ER33917 [09]ER34020 [11]ER34119 ///
           [13]ER34230 [15]ER34349

******************************
*** Family-Level Variables ***
******************************

********************************************************************
*** 1968 FAMILY IDENTIFIER                                       ***
*** ------------------------------------------------------------ ***
*** [69]V534    [70]V1230   [71]V1932   [72]V2533    [73]V3085   ***
*** [74]V3497   [75]V3909   [76]V4423   [77]V5336    [78]V5835   ***
*** [79]V6446   [80]V7050   [81]V7642   [82]V8335    [83]V8943   ***
*** [84]V10400  [85]V11581  [86]V12988  [87]V14090   [88]V15105  ***
*** [89]V16605  [90]V18021  [91]V19321  [92]V20621   [93]V22400  ***
*** [94]ER2005G [95]ER5005G [96]ER7005G [97]ER10005G [99]ER13019 ***
*** [01]ER17022 [03]ER21009 [05]ER25009 [07]ER36009  [09]ER42009 ***
*** [11]ER47309 [13]ER53009 [15]ER60009                          ***
***  ----------------------------------------------------------- ***
*** 1 - 2,930 Family from the 1968 SRC cross-section sample      ***
*** 3,001 - 3,511 Family from the Immigrant sample               ***
***               added in 1997 and 1999.                        ***
*** 5,001 - 6,872 Family from the 1968 Census sample.            ***
***                                                              ***
*** All families with the same 1968 ID contain at                ***
*** least one of the original members from the 1968              ***
*** family or their lineal descendents born after                ***
*** 1968.                                                        ***
********************************************************************

   renpsid f_FamilyID1968 ///
           [69]V534    [70]V1230   [71]V1932   [72]V2533    [73]V3085   ///
           [74]V3497   [75]V3909   [76]V4423   [77]V5336    [78]V5835   ///
           [79]V6446   [80]V7050   [81]V7642   [82]V8335    [83]V8943   ///
           [84]V10400  [85]V11581  [86]V12988  [87]V14090   [88]V15105  ///
           [89]V16605  [90]V18021  [91]V19321  [92]V20621   [93]V22400  ///
           [94]ER2005G [95]ER5005G [96]ER7005G [97]ER10005G [99]ER13019 ///
           [01]ER17022 [03]ER21009 [05]ER25009 [07]ER36009  [09]ER42009 ///
           [11]ER47309 [13]ER53009 [15]ER60009

*******************************************************************
*** FAMILY INTERVIEW (ID) NUMBER                                ***
*** ----------------------------------------------------------- ***
*** [68]V3      [69]V442    [70]V1102   [71]V1802   [72]V2402   ***
*** [73]V3002   [74]V3402   [75]V3802   [76]V4302   [77]V5202   ***
*** [78]V5702   [79]V6302   [80]V6902   [81]V7502   [82]V8202   ***
*** [83]V8802   [84]V10002  [85]V11102  [86]V12502  [87]V13702  ***
*** [88]V14802  [89]V16302  [90]V17702  [91]V19002  [92]V20302  ***
*** [93]V21602  [94]ER2002  [95]ER5002  [96]ER7002  [97]ER10002 ***
*** [99]ER13002 [01]ER17002 [03]ER21002 [05]ER25002 [07]ER36002 ***
*** [09]ER42002 [11]ER47302 [13]ER53002 [15]ER60002             ***
*** ----------------------------------------------------------- ***
*** Each family unit in a specific wave is assigned             ***
**  a unique "Family Interview (ID) Number" valid               ***
*** for that wave only.                                         ***
*******************************************************************

   renpsid f_FamilyID ///
           [68]V3      [69]V442    [70]V1102   [71]V1802   [72]V2402   ///
           [73]V3002   [74]V3402   [75]V3802   [76]V4302   [77]V5202   ///
           [78]V5702   [79]V6302   [80]V6902   [81]V7502   [82]V8202   ///
           [83]V8802   [84]V10002  [85]V11102  [86]V12502  [87]V13702  ///
           [88]V14802  [89]V16302  [90]V17702  [91]V19002  [92]V20302  ///
           [93]V21602  [94]ER2002  [95]ER5002  [96]ER7002  [97]ER10002 ///
           [99]ER13002 [01]ER17002 [03]ER21002 [05]ER25002 [07]ER36002 ///
           [09]ER42002 [11]ER47302 [13]ER53002 [15]ER60002

***********************************************************************
* Check that each family member has the same 1968 FAMILY IDENTIFIER ***
***********************************************************************

foreach yy of numlist 1969/1997 1999(2)2015 {
   bysort f_FamilyID_`yy' : ///
   assert f_FamilyID1968_`yy'==f_FamilyID1968_`yy'[1]
}

********************************************************************
*** Check that each FU has exactly one head and at most one wife ***
********************************************************************

foreach yy of numlist 1968/1997 1999(2)2015 {
  egen Num_Head = total(currentHead_`yy'), by(f_FamilyID_`yy')
  egen Num_Wife = total(currentWife_`yy'), by(f_FamilyID_`yy')
  assert Num_Head==1 if f_FamilyID_`yy'~=.
  assert (Num_Wife==0 | Num_Wife==1) if f_FamilyID_`yy'~=.
  drop Num_Head Num_Wife
}

*******************************************************************
*** NUMBER IN FU                                                ***
*** ----------------------------------------------------------- ***
*** [68]V115    [69]V549    [70]V1238   [71]V1941   [72]V2541   ***
*** [73]V3094   [74]V3507   [75]V3920   [76]V4435   [77]V5349   ***
*** [78]V5849   [79]V6461   [80]V7066   [81]V7657   [82]V8351   ***
*** [83]V8960   [84]V10418  [85]V11605  [86]V13010  [87]V14113  ***
*** [88]V15129  [89]V16630  [90]V18048  [91]V19348  [92]V20650  ***
*** [93]V22405  [94]ER2006  [95]ER5005  [96]ER7005  [97]ER10008 ***
*** [99]ER13009 [01]ER17012 [03]ER21016 [05]ER25016 [07]ER36016 ***
*** [09]ER42016 [11]ER47316 [13]ER53016 [15]ER60016             ***
*******************************************************************

   renpsid f_familysize ///
           [68]V115    [69]V549    [70]V1238   [71]V1941   [72]V2541   ///
           [73]V3094   [74]V3507   [75]V3920   [76]V4435   [77]V5349   ///
           [78]V5849   [79]V6461   [80]V7066   [81]V7657   [82]V8351   ///
           [83]V8960   [84]V10418  [85]V11605  [86]V13010  [87]V14113  ///
           [88]V15129  [89]V16630  [90]V18048  [91]V19348  [92]V20650  ///
           [93]V22405  [94]ER2006  [95]ER5005  [96]ER7005  [97]ER10008 ///
           [99]ER13009 [01]ER17012 [03]ER21016 [05]ER25016 [07]ER36016 ///
           [09]ER42016 [11]ER47316 [13]ER53016 [15]ER60016

*******************************************************************
*** SEX OF HEAD                                                 ***
*** ----------------------------------------------------------- ***
*** [68]V119    [69]V1010   [70]V1240   [71]V1943   [72]V2543   ***
*** [73]V3096   [74]V3509   [75]V3922   [76]V4437   [77]V5351   ***
*** [78]V5851   [79]V6463   [80]V7068   [81]V7659   [82]V8353   ***
*** [83]V8962   [84]V10420  [85]V11607  [86]V13012  [87]V14115  ***
*** [88]V15131  [89]V16632  [90]V18050  [91]V19350  [92]V20652  ***
*** [93]V22407  [94]ER2008  [95]ER5007  [96]ER7007  [97]ER10010 ***
*** [99]ER13011 [01]ER17014 [03]ER21018 [05]ER25018 [07]ER36018 ***
*** [09]ER42018 [11]ER47318 [13]ER53018 [15]ER60018             ***
*** ----------------------------------------------------------- ***
*** 1 Male                                                      ***
*** 2 Female                                                    ***
*******************************************************************

   renpsid h_sex ///
           [68]V119    [69]V1010   [70]V1240   [71]V1943   [72]V2543   ///
           [73]V3096   [74]V3509   [75]V3922   [76]V4437   [77]V5351   ///
           [78]V5851   [79]V6463   [80]V7068   [81]V7659   [82]V8353   ///
           [83]V8962   [84]V10420  [85]V11607  [86]V13012  [87]V14115  ///
           [88]V15131  [89]V16632  [90]V18050  [91]V19350  [92]V20652  ///
           [93]V22407  [94]ER2008  [95]ER5007  [96]ER7007  [97]ER10010 ///
           [99]ER13011 [01]ER17014 [03]ER21018 [05]ER25018 [07]ER36018 ///
           [09]ER42018 [11]ER47318 [13]ER53018 [15]ER60018

*******************************************************************
*** Total Family Money Income 1968 - 2015                       ***
*** ----------------------------------------------------------- ***
*** [68]V81     [69]V529    [70]V1514   [71]V2226   [72]V2852   ***
*** [73]V3256   [74]V3676   [75]V4154   [76]V5029   [77]V5626   ***
*** [78]V6173   [79]V6766   [80]V7412   [81]V8065   [82]V8689   ***
*** [83]V9375   [84]V11022  [85]V12371  [86]V13623  [87]V14670  ***
*** [88]V16144  [89]V17533  [90]V18875  [91]V20175  [92]V21481  ***
*** [93]V23322  [94]ER4153  [95]ER6993  [96]ER9244  [97]ER12079 ***
*** [99]ER16462 [01]ER20456 [03]ER24099 [05]ER28037 [07]ER41027 ***
*** [09]ER46935 [11]ER52343 [13]ER58152 [15]ER65349             ***
*** ----------------------------------------------------------- ***
*** -999,999        Loss of $999,999 or more                    ***
*** -999,998 - -1   Actual loss                                 ***
*** 0               No family money income in 1995              ***
*** 1 - 9,999,998   Actual amount                               ***
*******************************************************************

   renpsid f_faminc ///
           [68]V81     [69]V529    [70]V1514   [71]V2226   [72]V2852   ///
           [73]V3256   [74]V3676   [75]V4154   [76]V5029   [77]V5626   ///
           [78]V6173   [79]V6766   [80]V7412   [81]V8065   [82]V8689   ///
           [83]V9375   [84]V11022  [85]V12371  [86]V13623  [87]V14670  ///
           [88]V16144  [89]V17533  [90]V18875  [91]V20175  [92]V21481  ///
           [93]V23322  [94]ER4153  [95]ER6993  [96]ER9244  [97]ER12079 ///
           [99]ER16462 [01]ER20456 [03]ER24099 [05]ER28037 [07]ER41027 ///
           [09]ER46935 [11]ER52343 [13]ER58152 [15]ER65349

********************************************************************
*** LABOR INCOME OF HEAD                                         ***
*** ------------------------------------------------------------ ***
*** [68]V251    [69]V699    [70]V1191   [71]V1892   [72]V2493    ***
*** [73]V3046   [74]V3458   [75]V3858   [76]V4373   [77]V5283    ***
*** [78]V5782   [79]V6391   [80]V6981   [81]V7573   [82]V8265    ***
*** [83]V8873   [84]V10256  [85]V11397  [86]V12796  [87]V13898   ***
*** [88]V14913  [89]V16413  [90]V17829  [91]V19129  [92]V20429   ***
*** [93]V21739  [94]ER4122  [95]ER6962  [96]ER9213  [97]ER12196  ***
*** [99]ER16493 [01]ER20425 [03]ER24117 [05]ER27913 [07]ER40903  ***
*** [09]ER46811 [11]ER52219 [13]ER58020 [15]ER65200              ***
*** ------------------------------------------------------------ ***
*** 1968 - 1969:                                                 ***
*** 0 None                                                       ***
*** 1  $1 - 499                                                  ***
*** 2  $500 - 999                                                ***
*** 3  $1000 - 1999                                              ***
*** 4  $2000 - 2999                                              ***
*** 5  $3000 - 4999                                              ***
*** 6  $5000 - 7499                                              ***
*** 7  $7500 - 9999                                              ***
*** 8  $10,000 or more                                           ***
*** ------------------------------------------------------------ ***
*** 1970 +                                                       ***
*** 1 - 9,999,998   Actual Amount                                ***
*** 9,999,999       $9,999,999 or more                           ***
*** 0               Inap.: no labor income                       ***
********************************************************************

   renpsid h_income ///
           [68]V251    [69]V699    [70]V1191   [71]V1892   [72]V2493   ///
           [73]V3046   [74]V3458   [75]V3858   [76]V4373   [77]V5283   ///
           [78]V5782   [79]V6391   [80]V6981   [81]V7573   [82]V8265   ///
           [83]V8873   [84]V10256  [85]V11397  [86]V12796  [87]V13898  ///
           [88]V14913  [89]V16413  [90]V17829  [91]V19129  [92]V20429  ///
           [93]V21739  [94]ER4122  [95]ER6962  [96]ER9213  [97]ER12196 ///
           [99]ER16493 [01]ER20425 [03]ER24117 [05]ER27913 [07]ER40903 ///
           [09]ER46811 [11]ER52219 [13]ER58020 [15]ER65200

********************************************************************
*** LABOR INCOME OF WIFE                                         ***
*** ------------------------------------------------------------ ***
*** [68]V75     [69]V516    [70]V1198   [71]V1899   [72]V2500    ***
*** [73]V3053   [74]V3465   [75]V3865   [76]V4379   [77]V5289    ***
*** [78]V5788   [79]V6398   [80]V6988   [81]V7580   [82]V8273    ***
*** [83]V8881   [84]V10263  [85]V11404  [86]V12803  [87]V13905   ***
*** [88]V14920  [89]V16420  [90]V17836  [91]V19136  [92]V20436   ***
*** [93]V23324  [94]ER4144  [95]ER6984  [96]ER9235  [97]ER12082  ***
*** [99]ER16465 [01]ER20447 [03]ER24135 [05]ER27943 [07]ER40933  ***
*** [09]ER46841 [11]ER52249 [13]ER58050 [15]ER65244              ***
*** ------------------------------------------------------------ ***
*** 1 - 9,999,998   Actual Amount                                ***
*** 9,999,999       Latino sample family                         ***
*** 0               Inap.: no wife/"wife" in FU; no labor income ***
********************************************************************

   renpsid w_income ///
           [68]V75     [69]V516    [70]V1198   [71]V1899   [72]V2500   ///
           [73]V3053   [74]V3465   [75]V3865   [76]V4379   [77]V5289   ///
           [78]V5788   [79]V6398   [80]V6988   [81]V7580   [82]V8273   ///
           [83]V8881   [84]V10263  [85]V11404  [86]V12803  [87]V13905  ///
           [88]V14920  [89]V16420  [90]V17836  [91]V19136  [92]V20436  ///
           [93]V23324  [94]ER4144  [95]ER6984  [96]ER9235  [97]ER12082 ///
           [99]ER16465 [01]ER20447 [03]ER24135 [05]ER27943 [07]ER40933 ///
           [09]ER46841 [11]ER52249 [13]ER58050 [15]ER65244

**************************************************************
*** EDUCATION OF HEAD 1968 - 1990                          ***
*** ------------------------------------------------------ ***
*** [68]V313   [69]V794   [70]V1485  [71]V2197  [72]V2823  ***
*** [73]V3241  [74]V3663  [75]V4198  [76]V5074  [77]V5647  ***
*** [78]V6194  [79]V6787  [80]V7433  [81]V8085  [82]V8709  ***
*** [83]V9395  [84]V11042 [85]V12400 [86]V13640 [87]V14687 ***
*** [88]V16161 [89]V17545 [90]V18898                       ***
**************************************************************
*** EDUCATION 1985 WIFE 1968 - 1990                        ***
*** ------------------------------------------------------ ***
*** [68]V246   [72]V2687  [73]V3216  [74]V3638  [75]V4199  ***
*** [76]V5075  [77]V5648  [78]V6195  [79]V6788  [80]V7434  ***
*** [81]V8086  [82]V8710  [83]V9396  [84]V11043 [85]V12401 ***
*** [86]V13641 [87]V14688 [88]V16162 [89]V17546 [90]V18899 ***
*** ------------------------------------------------------ ***
*** 1   0 - 5 grades (or mentions could not read or write) ***
*** 2   6 - 8 grades, grade school, DK but mentions could  ***
***           read or write                                ***
*** 3   9 - 11 grades, some high school, junior high       ***
*** 4   12 grades, high school                             ***
*** 5   12 grades plus non-academic training               ***
*** 6   College, no degree                                 ***
*** 7   College degree, no advanced degree mentioned       ***
*** 8   College, advanced or professional degree           ***
*** 9   NA, DK to all L9 - L15a                            ***
*** 0   Could not read or write / DK grade and could not   ***
***           read or write                                ***
*** 0   Inap.: no Wife/"Wife" in FU (V11608=00)            ***
**************************************************************

   renpsid h_educ ///
           [68]V313   [69]V794   [70]V1485  [71]V2197  [72]V2823  ///
           [73]V3241  [74]V3663  [75]V4198  [76]V5074  [77]V5647  ///
           [78]V6194  [79]V6787  [80]V7433  [81]V8085  [82]V8709  ///
           [83]V9395  [84]V11042 [85]V12400 [86]V13640 [87]V14687 ///
           [88]V16161 [89]V17545 [90]V18898

   renpsid w_educ ///
           [68]V246   [72]V2687  [73]V3216  [74]V3638  [75]V4199  ///
           [76]V5075  [77]V5648  [78]V6195  [79]V6788  [80]V7434  ///
           [81]V8086  [82]V8710  [83]V9396  [84]V11043 [85]V12401 ///
           [86]V13641 [87]V14688 [88]V16162 [89]V17546 [90]V18899

*******************************************************************
*** COMPLETED ED HEAD 1975 - 2015                               ***
*** ----------------------------------------------------------- ***
*** [75]V4093   [76]V4684   [77]V5608   [78]V6157   [79]V6754   ***
*** [80]V7387   [81]V8039   [82]V8663   [83]V9349   [84]V10996  ***
*** [91]V20198  [92]V21504  [93]V23333  [94]ER4158  [95]ER6998  ***
*** [96]ER9249  [97]ER12222 [99]ER16516 [01]ER20457 [03]ER24148 ***
*** [05]ER28047 [07]ER41037 [09]ER46981 [11]ER52405 [13]ER58223 ***
*** [15]ER65459                                                 ***
*******************************************************************
*** COMPLETED ED WIFE 1975 - 2015                               ***
*** ----------------------------------------------------------- ***
*** [75]V4102   [76]V4695   [77]V5567   [78]V6116   [79]V6713   ***
*** [80]V7346   [81]V7998   [82]V8622   [83]V9308   [84]V10955  ***
*** [91]V20199  [92]V21505  [93]V23334  [94]ER4159  [95]ER6999  ***
*** [96]ER9250  [97]ER12223 [99]ER16517 [01]ER20458 [03]ER24149 ***
*** [05]ER28048 [07]ER41038 [09]ER46982 [11]ER52406 [13]ER58224 ***
*** [15]ER65460                                                 ***
*** ----------------------------------------------------------- ***
*** 1 - 16  Actual grade of school completed                    ***
*** 17      Completed at least some postgraduate work           ***
*** 99      NA / DK                                             ***
*** 0       Inap: completed no grades of school                 ***
*** 0       Inap.: no wife/"wife" in FU / completed no grades   ***
***         of school                                           ***
*******************************************************************

   renpsid h_completEDUC ///
           [75]V4093   [76]V4684   [77]V5608   [78]V6157   [79]V6754   ///
           [80]V7387   [81]V8039   [82]V8663   [83]V9349   [84]V10996  ///
           [91]V20198  [92]V21504  [93]V23333  [94]ER4158  [95]ER6998  ///
           [96]ER9249  [97]ER12222 [99]ER16516 [01]ER20457 [03]ER24148 ///
           [05]ER28047 [07]ER41037 [09]ER46981 [11]ER52405 [13]ER58223 ///
           [15]ER65459

   renpsid w_completEDUC ///
           [75]V4102   [76]V4695   [77]V5567   [78]V6116   [79]V6713   ///
           [80]V7346   [81]V7998   [82]V8622   [83]V9308   [84]V10955  ///
           [91]V20199  [92]V21505  [93]V23334  [94]ER4159  [95]ER6999  ///
           [96]ER9250  [97]ER12223 [99]ER16517 [01]ER20458 [03]ER24149 ///
           [05]ER28048 [07]ER41038 [09]ER46982 [11]ER52406 [13]ER58224 ///
           [15]ER65460

*******************************************************************
*** RACE OF HEAD 1st, 2nd, 3rd MENTION                          ***
*** ----------------------------------------------------------- ***
*** [68]V181    [69]V801    [70]V1490   [71]V2202   [72]V2828   ***
*** [73]V3300   [74]V3720   [75]V4204   [76]V5096   [77]V5662   ***
*** [78]V6209   [79]V6802   [80]V7447   [81]V8099   [82]V8723   ***
*** [83]V9408   [84]V11055  [85]V11938  [86]V13565  [87]V14612  ***
*** [88]V16086  [89]V17483  [90]V18814  [91]V20114  [92]V21420  ***
*** [93]V23276  [94]ER3944  [95]ER6814  [96]ER9060  [97]ER11848 ***
*** [99]ER15928 [01]ER19989 [03]ER23426 [05]ER27393 [07]ER40565 ***
*** [09]ER46543 [11]ER51904 [13]ER57659 [15]ER64810             ***
***                                                             ***
*** [85]V11939  [86]V13566  [87]V14613  [88]V16087  [89]V17484  ***
*** [90]V18815  [91]V20115  [92]V21421  [93]V23277  [94]ER3945  ***
*** [95]ER6815  [96]ER9061  [97]ER11849 [99]ER15929 [01]ER19990 ***
*** [03]ER23427 [05]ER27394 [07]ER40566 [09]ER46544 [11]ER51905 ***
*** [13]ER57660 [15]ER64811                                     ***
***                                                             ***
*** [94]ER3946  [95]ER6816  [96]ER906 2 [97]ER11850 [99]ER15930 ***
*** [01]ER19991 [03]ER23428 [05]ER27395 [07]ER40567 [09]ER46545 ***
*** [11]ER51906 [13]ER57661 [15]ER64812                         ***
*******************************************************************
*** RACE OF WIFE 1st, 2nd, 3rd MENTION 96                       ***
*** ----------------------------------------------------------- ***
*** [85]V12293  [86]V13500  [87]V14547  [88]V16021  [89]V17418  ***
*** [90]V18749  [91]V20049  [92]V21355  [93]V23212  [94]ER3883  ***
*** [95]ER6753  [96]ER8999  [97]ER11760 [99]ER15836 [01]ER19897 ***
*** [03]ER23334 [05]ER27297 [07]ER40472 [09]ER46449 [11]ER51810 ***
*** [13]ER57549 [15]ER64671                                     ***
***                                                             ***
*** [85]V12294  [86]V13501  [87]V14548  [88]V16022  [89]V17419  ***
*** [90]V18750  [91]V20050  [92]V21356  [93]V23213  [94]ER3884  ***
*** [95]ER6754  [96]ER9000  [97]ER11761 [99]ER15837 [01]ER19898 ***
*** [03]ER23335 [05]ER27298 [07]ER40473 [09]ER46450 [11]ER51811 ***
*** [13]ER57550 [15]ER64672                                     ***
***                                                             ***
*** [94]ER3885  [95]ER6755  [96]ER9001  [97]ER11762 [99]ER15838 ***
*** [01]ER19899 [03]ER23336 [05]ER27299 [07]ER40474 [09]ER46451 ***
*** [11]ER51812 [13]ER57551 [15]ER64673                         ***
*** ----------------------------------------------------------- ***
*** 1968 to 1984:                                               ***
*** 1  White                                                    ***
*** 2  Black                                                    ***
*** 3  Puerto Rican, Mexican, Cuban, Spanish American           ***
*** 7  Other                                                    ***
*** 9  NA, DK                                                   ***
*** 0  Inap. No wife                                            ***
***                                                             ***
*** 1985 to 1989:                                               ***
*** 1  White                                                    ***
*** 2  Black                                                    ***
*** 3  American Indian, Aleut, Eskimo                           ***
*** 4  Asian, Pacific Islander                                  ***
*** 7  Other                                                    ***
*** 9  NA, DK                                                   ***
*** 0  Inap. No wife                                            ***
***                                                             ***
*** 1990 to 2003:                                               ***
*** 1  White                                                    ***
*** 2  Black                                                    ***
*** 3  American Indian, Aleut, Eskimo                           ***
*** 4  Asian, Pacific Islander                                  ***
*** 5  Mentions Latino origin or descent                        ***
*** 6  Mentions color other than black or white                 ***
*** 7  Other                                                    ***
*** 9  NA, DK                                                   ***
*** 0  Inap. No wife                                            ***
***                                                             ***
*** 2005 to 2015:                                               ***
*** 1  White                                                    ***
*** 2  Black                                                    ***
*** 3  American Indian, Aleut, Eskimo                           ***
*** 4  Asian, Pacific Islander                                  ***
*** 5  Native Hawaiian or Pacific Islander                      ***
*** 7  Other                                                    ***
*** 9  NA, DK                                                   ***
*** 0  Inap. No wife                                            ***
*******************************************************************

   renpsid h_race1 ///
           [68]V181    [69]V801    [70]V1490   [71]V2202   [72]V2828   ///
           [73]V3300   [74]V3720   [75]V4204   [76]V5096   [77]V5662   ///
           [78]V6209   [79]V6802   [80]V7447   [81]V8099   [82]V8723   ///
           [83]V9408   [84]V11055  [85]V11938  [86]V13565  [87]V14612  ///
           [88]V16086  [89]V17483  [90]V18814  [91]V20114  [92]V21420  ///
           [93]V23276  [94]ER3944  [95]ER6814  [96]ER9060  [97]ER11848 ///
           [99]ER15928 [01]ER19989 [03]ER23426 [05]ER27393 [07]ER40565 ///
           [09]ER46543 [11]ER51904 [13]ER57659 [15]ER64810

   renpsid h_race2 ///
           [85]V11939  [86]V13566  [87]V14613  [88]V16087  [89]V17484  ///
           [90]V18815  [91]V20115  [92]V21421  [93]V23277  [94]ER3945  ///
           [95]ER6815  [96]ER9061  [97]ER11849 [99]ER15929 [01]ER19990 ///
           [03]ER23427 [05]ER27394 [07]ER40566 [09]ER46544 [11]ER51905 ///
           [13]ER57660 [15]ER64811

   renpsid h_race3 ///
           [94]ER3946  [95]ER6816  [96]ER9062  [97]ER11850 [99]ER15930 ///
           [01]ER19991 [03]ER23428 [05]ER27395 [07]ER40567 [09]ER46545 ///
           [11]ER51906 [13]ER57661 [15]ER64812

   renpsid w_race1 ///
           [85]V12293  [86]V13500  [87]V14547  [88]V16021  [89]V17418  ///
           [90]V18749  [91]V20049  [92]V21355  [93]V23212  [94]ER3883  ///
           [95]ER6753  [96]ER8999  [97]ER11760 [99]ER15836 [01]ER19897 ///
           [03]ER23334 [05]ER27297 [07]ER40472 [09]ER46449 [11]ER51810 ///
           [13]ER57549 [15]ER64671

   renpsid w_race2 ///
           [85]V12294  [86]V13501  [87]V14548  [88]V16022  [89]V17419  ///
           [90]V18750  [91]V20050  [92]V21356  [93]V23213  [94]ER3884  ///
           [95]ER6754  [96]ER9000  [97]ER11761 [99]ER15837 [01]ER19898 ///
           [03]ER23335 [05]ER27298 [07]ER40473 [09]ER46450 [11]ER51811 ///
           [13]ER57550 [15]ER64672

   renpsid w_race3 ///
           [94]ER3885  [95]ER6755  [96]ER9001  [97]ER11762 [99]ER15838 ///
           [01]ER19899 [03]ER23336 [05]ER27299 [07]ER40474 [09]ER46451 ///
           [11]ER51812 [13]ER57551 [15]ER64673

**************************************************************
*** Check that the variables "h_race1_yy" and "w_race1_yy" ***
*** take on the expected values in each year.              ***
**************************************************************

foreach yy of numlist 1968/1997 1999(2)2015 {
   assert inlist(h_race1_`yy',1,2,3,7,9,.)       if `yy'>=1968 & `yy'<=1984
   assert inlist(h_race1_`yy',1,2,3,4,7,9,.)     if `yy'>=1985 & `yy'<=1989
   assert inlist(h_race1_`yy',1,2,3,4,5,6,7,9,.) if `yy'>=1990 & `yy'<=2004
   assert inlist(h_race1_`yy',0,1,2,3,4,5,7,9,.) if `yy'>=2005
   quietly tab h_race1_`yy' if h_race1_`yy'~=9 & h_race1_`yy'~=0
   assert r(r)==4 if `yy'>=1968 & `yy'<=1984
   assert r(r)==5 if `yy'>=1985 & `yy'<=1989
   assert r(r)==7 if `yy'>=1990 & `yy'<=2004
   assert r(r)==6 if `yy'>=2005
}

foreach yy of numlist 1985/1997 1999(2)2015 {
   di "`yy'"
   assert inlist(w_race1_`yy',0,1,2,3,4,7,9,.)     if `yy'>=1985 & `yy'<=1989
   assert inlist(w_race1_`yy',0,1,2,3,4,5,6,7,9,.) if `yy'>=1990 & `yy'<=2004
   assert inlist(w_race1_`yy',0,1,2,3,4,5,7,9,.) if `yy'>=2005
   quietly tab w_race1_`yy' if w_race1_`yy'~=9 & w_race1_`yy'~=0
   assert r(r)==4 if `yy'>=1968 & `yy'<=1984
   assert r(r)==5 if `yy'>=1985 & `yy'<=1989
   assert r(r)==7 if `yy'>=1990 & `yy'<=2004
   assert r(r)==6 if `yy'>=2005
}

**********************************************************************
*** Race is asked in the PSID family level variables for the head  ***
*** and the spouse.  It is not asked in the individual file.  The  ***
*** variable "ownrace" is the person's last (i.e., most recent)    ***
*** reported race, other than NA/DK, if they have ever been the    ***
*** head or wife (otherwise missing).  All three of the "ownrace"  ***
*** variables come from the same year, based on the last time      ***
*** a valid "race1" was reported).                                 ***
*** -------------------------------------------------------------- ***
*** Note: race was re-asked when the race question changed, but it ***
***       was not asked every year.  The PSID carried over the     ***
***       the race question if race was already known.             ***
**********************************************************************

   gen ownrace1 = .
   gen ownrace2 = .
   gen ownrace3 = .
   gen ownrace_year = .
   label variable ownrace1      "Individuals latest report of own race1"
   label variable ownrace2      "Individuals latest report of own race2"
   label variable ownrace3      "Individuals latest report of own race3"
   label variable ownrace_year  "Latest year individual reported own race"

* Latest report of race as head or wife
foreach yy of numlist 2015(2)1999 1997/1985 {
   global userace = "currentHead_`yy'==1 & ownrace1==. & inlist(h_race1_`yy',1,2,3,4,5,6,7)"
   if `yy'>=1994 {
      replace ownrace3      =  h_race3_`yy' if $userace
   }
   if `yy'>=1985 {
      replace ownrace2      =  h_race2_`yy' if $userace
   }
   replace ownrace_year     = `yy'          if $userace
   replace ownrace1         =  h_race1_`yy' if $userace
   macro drop userace

   global userace "currentWife_`yy'==1 & ownrace1==. & inlist(w_race1_`yy',1,2,3,4,5,6,7)"
   if `yy'>=1994 {
      replace ownrace3      =  w_race3_`yy' if $userace
   }
   if `yy'>=1985 {
      replace ownrace2      =  w_race2_`yy' if $userace
   }
   replace ownrace_year     = `yy'          if $userace
   replace ownrace1         =  w_race1_`yy' if $userace
   macro drop userace
}

* Latest report of race as head (note: wife race not reported until 1985)
foreach yy of numlist 1984/1968 {
   global userace "currentHead_`yy'==1 & ownrace1==. & inlist(h_race1_`yy',1,2,3,4,5,6,7)"
   if `yy'>=1994 {
      replace ownrace3      =  h_race3_`yy' if $userace
   }
   if `yy'>=1985 {
      replace ownrace2      =  h_race2_`yy' if $userace
   }
   replace ownrace_year     = `yy'          if $userace
   replace ownrace1         =  h_race1_`yy' if $userace
   macro drop userace
}

**********************************************************************
*** Variable "headrace" is the person's last (i.e., most recent)   ***
*** reported head's race, other than NA/DK, if they have ever      ***
*** been related to (child, parent, sibling) the a head            ***
*** (otherwise missing).                                           ***
**********************************************************************

   gen headrace1 = .
   gen headrace2 = .
   gen headrace3 = .
   gen headrace_year = .
   label variable headrace1      "Latest heads race1 when related to head"
   label variable headrace2      "Latest heads race2 when related to head"
   label variable headrace3      "Latest heads race3 when related to head"
   label variable headrace_year  "Latest year individual was related to head"

foreach yy of numlist 2015(2)1999 1997/1968 {
   global userace = "relatedToHead_`yy'==1 & headrace1==. & inlist(h_race1_`yy',1,2,3,4,5,6,7)"
   if `yy'>=1994 {
      replace headrace3      =  h_race3_`yy' if $userace
   }
   if `yy'>=1985 {
      replace headrace2      =  h_race2_`yy' if $userace
   }
   replace headrace_year     = `yy'          if $userace
   replace headrace1         =  h_race1_`yy' if $userace
   macro drop userace
}

*******************************************************************
*** SPANISH DESCENT 1 HEAD (1985-1996 and 2005-2015)            ***
*** (There is no added value to using 2nd/3rd mention)          ***
*** ----------------------------------------------------------- ***
*** [85]V11937  [86]V13564  [87]V14611  [88]V16085  [89]V17482  ***
*** [90]V18813  [91]V20113  [92]V21419  [93]V23275  [94]ER3941  ***
*** [95]ER6811  [96]ER9057  [05]ER27392 [07]ER40564 [09]ER46542 ***
*** [11]ER51903 [13]ER57658 [15]ER64809                         ***
*******************************************************************
*** SPANISH DESCENT 1 WIFE                                      ***
*** (There is no added value to using 2nd/3rd mention)          ***
*** ----------------------------------------------------------- ***
*** [85]V12292  [86]V13499  [87]V14546  [88]V16020  [89]V17417  ***
*** [90]V18748  [91]V20048  [92]V21354  [93]V23211  [94]ER3880  ***
*** [95]ER6750  [96]ER8996  [05]ER27296 [07]ER40471 [09]ER46448 ***
*** [11]ER51809 [13]ER57548 [15]ER64670                         ***
*** ----------------------------------------------------------- ***
*** 1 Mexican                                                   ***
*** 2 Mexican American                                          ***
*** 3 Chicano                                                   ***
*** 4 Puerto Rican                                              ***
*** 5 Cuban                                                     ***
*** 6 Combinations, more than one mention (1985-1996)           ***
*** 7 Other Spanish, Hispanic, Latino                           ***
*** 9 NA, refused                                               ***
*** 0 Inap.: not Spanish, Hispanic or Latino (no wife)          ***
*******************************************************************

   renpsid h_spanish1 ///
           [85]V11937  [86]V13564  [87]V14611  [88]V16085  [89]V17482  ///
           [90]V18813  [91]V20113  [92]V21419  [93]V23275  [94]ER3941  ///
           [95]ER6811  [96]ER9057  [05]ER27392 [07]ER40564 [09]ER46542 ///
           [11]ER51903 [13]ER57658 [15]ER64809

   renpsid w_spanish1 ///
           [85]V12292  [86]V13499  [87]V14546  [88]V16020  [89]V17417  ///
           [90]V18748  [91]V20048  [92]V21354  [93]V23211  [94]ER3880  ///
           [95]ER6750  [96]ER8996  [05]ER27296 [07]ER40471 [09]ER46448 ///
           [11]ER51809 [13]ER57548 [15]ER64670

********************************************************************
*** Check that the variables "h_spanish1_yy" and "w_spanish1_yy" ***
*** take on the expected values in each year.                    ***
********************************************************************

foreach yy of numlist 1985/1996 2005(2)2015 {
   assert inlist(h_spanish1_`yy',1,2,3,4,5,6,7,9,0,.) if `yy'>=1985 & `yy'<=1996
   assert inlist(h_spanish1_`yy',1,2,3,4,5,7,9,0,.)   if `yy'>=2005 & `yy'<=2015
   quietly tab h_spanish1_`yy'
   assert r(r)==9 if `yy'>=1985 & `yy'<=1996
   assert r(r)==8 if `yy'>=2005 & `yy'<=2015

   assert inlist(w_spanish1_`yy',1,2,3,4,5,6,7,9,0,.) if `yy'>=1985 & `yy'<=1996
   assert inlist(w_spanish1_`yy',1,2,3,4,5,7,9,0,.)   if `yy'>=2005 & `yy'<=2015
   quietly tab w_spanish1_`yy'
   assert r(r)==9 if `yy'>=1985 & `yy'<=1996
   assert r(r)==8 if `yy'>=2005 & `yy'<=2015
}

***************************************************************************
*** SPANISH DESCENT is asked in the PSID family level variables for     ***
*** the head and the spouse.  It is not asked in the individual file.   ***
*** The variable "ownspanish" is the person's last (i.e., most recent)  ***
*** report of ethnicity, other than DK/NA, if they have ever been the   ***
*** head or wife(otherwise missing).                                    ***
***************************************************************************

   gen ownspanish = .
   gen ownspanish_year = .
   label variable ownspanish      "Individuals latest report of own ethnicity"
   label variable ownspanish_year "Year individual latest reported own ethnicity"

* Latest report of ethnicity as head or wife
foreach yy of numlist 2015(2)2005 1996/1985 {
   replace ownspanish_year = `yy'             if currentWife_`yy'==1 & ///
                              ownspanish==. & inlist(w_spanish1_`yy',0,1,2,3,4,5,6,7)
   replace ownspanish =       w_spanish1_`yy' if currentWife_`yy'==1 & ///
                              ownspanish==. & inlist(w_spanish1_`yy',0,1,2,3,4,5,6,7)
    replace ownspanish_year = `yy'            if currentHead_`yy'==1 & ///
                              ownspanish==. & inlist(h_spanish1_`yy',0,1,2,3,4,5,6,7)
   replace ownspanish =       h_spanish1_`yy' if currentHead_`yy'==1 & ///
                              ownspanish==. & inlist(h_spanish1_`yy',0,1,2,3,4,5,6,7)
}

***********************************************************************
*** Variable "headspanish" is the person's last (i.e., most recent) ***
*** reported head's ethnicity, other than NA/DK, if they have ever  ***
*** been related to (child, parent, or sibling) the head            ***
*** (otherwise missing).                                            ***
***********************************************************************

   gen headspanish = .
   gen headspanish_year = .
   label variable headspanish      "Latest heads spanish1 when related to head"
   label variable headspanish_year  "Latest year individual was related to head"

foreach yy of numlist 2015(2)2005 1996/1985 {
   global userace = "relatedToHead_`yy'==1 & headspanish==. & inlist(h_spanish1_`yy',1,2,3,4,5,6,7)"
   replace headrace_year     = `yy'          if $userace
   replace headspanish       =  h_spanish1_`yy' if $userace
   macro drop userace
}

************************************
*** Drop Original PSID Variables ***
************************************

   drop V* ER*

********************************************************************
*** Use merge to attach variables from natural mother and father ***
*** ------------------------------------------------------------ ***
*** Step 1: Save a dataset of everyone in the PSID (potential    ***
***         mothers and fathers).  Rename "person_ID" to         ***
***         "mother_ID" and "father_ID", and add "mom_" and      ***
***         "dad_" prefix to selected variables.                 ***
***                                                              ***
*** Step 2: Merge selected mother and father variables to        ***
***         the individuals in the "psid-HeadStart_d1.dta"       ***
***         Data.                                                ***
***                                                              ***
*** Note: "mother_ID"/"father_ID" can be missing or can take     ***
***       on a unique value for a parent who is not in the       ***
***       PSID (not all individual with a mother_ID are          ***
***       expected to match to their mother).                    ***
***                                                              ***
***       The "mom_" and "dad_" variables are missing if         ***
***       there is no mother/father in the PSID.                 ***
***                                                              ***
********************************************************************

#delimit ;
   global parentVars "female i_age_1995 i_yearseduc_*
                      ownrace1 ownrace2 ownrace3 ownrace_year
                      headrace1 headrace2 headrace3 headrace_year
                      ownspanish ownspanish_year
                      headspanish headspanish_year";
#delimit cr;

preserve

keep $parentVars person_ID

clonevar mother_ID = person_ID
clonevar father_ID = person_ID

foreach var of varlist $parentVars {
clonevar mom_`var' = `var'
clonevar dad_`var' = `var'
local lbl : variable label `var'
label variable mom_`var' `"(Mom) `lbl'"'
label variable dad_`var' `"(Dad) `lbl'"' 
}

drop $parentVars person_ID

save psid-HeadStart_d1_parent.dta, replace


restore 

merge m:1 mother_ID using psid-HeadStart_d1_parent.dta, keepusing(mom_*) keep(match master)
drop _merge
merge m:1 father_ID using psid-HeadStart_d1_parent.dta, keepusing(dad_*) keep(match master)
drop _merge

assert mom_female==1 | mom_female==.
assert dad_female==0 | dad_female==.


*****************
*** Save Data ***
*****************

   d,s
   compress
   d,s
   save psid-HeadStart_d1.dta, replace

log close
set more on

